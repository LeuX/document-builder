buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.6'
    }
}

plugins {
    id 'org.ajoberstar.github-pages' version '1.1.0'
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'groovy'
    targetCompatibility = 1.6

    apply plugin: 'codenarc'
    apply plugin: 'cobertura'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.9'
        testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
        testCompile 'cglib:cglib-nodep:3.1'
        testCompile 'org.objenesis:objenesis:2.1'
        codenarc 'org.codenarc:CodeNarc:0.23'
    }

    codenarc {
        toolVersion = '0.23'
        configFile = file("${rootProject.projectDir}/config/codenarc/rules.groovy")
    }

    cobertura {
        coverageSourceDirs = [sourceSets.main.allSource.srcDirs]
        coverageExcludes = [/.*com\.craigburke\.document\.core\.test.*/, /.*com\.craigburke\.document\.core\.Align.*/]
    }

    group = 'com.craigburke.document'
    version = '0.4.8'
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    configurations {
        provided
        testCompile { extendsFrom provided }
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    afterEvaluate { Project project ->
        bintray {
            user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : ''
            key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
            publications = ['maven']

            pkg {
                repo = 'document-builder'
                userOrg = 'craigburke'
                name = project.artifactId
            }
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    artifactId project.artifactId
                    pom.withXml {
                        asNode().children().last() + {
                            resolveStrategy = Closure.DELEGATE_FIRST
                            name project.artifactId
                            description project.description
                            url 'https://github.com/craigburke/document-builder'
                            scm {
                                url 'https://github.com/craigburke/document-builder'
                                connection 'scm:https://github.com/craigburke/document-builder.git'
                                developerConnection 'scm:https://github.com/craigburke/document-builder.git'
                            }
                            licenses {
                                license {
                                    name project.license.name
                                    url project.license.url
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'craigburke'
                                    name 'Craig Burke'
                                    email 'craig@craigburke.com'
                                }
                            }
                        }
                    }
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
            }
        }
    }

    bintrayUpload.dependsOn build, sourcesJar, javadoc

    task release {
        dependsOn bintrayUpload, publishGhPages
    }
}

asciidoctor {
    sourceDir = file('docs')

    resources {
        from(sourceDir) {
            include 'css/**', 'images/**'
        }
    }

    attributes 'source-highlighter': 'prettify',
            'docinfo1': ['version': project.version],
            'imagesdir': 'images',
            'stylesdir': 'css',
            icons: 'font',
            'toc': 'left',
            version: project.version,
            'projectUrl': 'https://github.com/craigburke/document-builder'
}

githubPages {
    repoUri = 'https://github.com/craigburke/document-builder.git'

    credentials {
        username = project.hasProperty('githubToken') ? project.githubToken : ''
        password = ''
    }

    pages {
        from file(asciidoctor.outputDir.path + '/html5')
    }
}

publishGhPages.dependsOn asciidoctor

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
